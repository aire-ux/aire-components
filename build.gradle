

plugins {
    id 'pmd'
    id 'groovy'
    id 'com.vaadin'
    id 'java-library'
//    id 'java-test-fixtures'
    id "com.github.spotbugs"
    id "io.freefair.lombok"
    id "com.diffplug.spotless"
    id "com.github.node-gradle.node"
    id "io.spring.dependency-management"
}
//apply from: "$rootDir/tasks/revisions.gradle"


allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'


    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url "https://maven.vaadin.com/vaadin-addons"
        }
        maven {
            url "https://repository.mulesoft.org/nexus/content/repositories/public/"
        }

        maven {
            url "https://maven.pkg.github.com/sunshower-io/sunshower-arcus"
            credentials {
                username "$mavenRepositoryUsername"
                password "$mavenRepositoryPassword"
            }
        }

        maven {
            url "https://maven.pkg.github.com/sunshower-io/*"
            credentials {
                username "$mavenRepositoryUsername"
                password "$mavenRepositoryPassword"
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom "com.aire-ux.components:bom-imported:$version"
        }
    }

    configurations {

        [
                "com.vaadin.webjar",
                "org.webjars.bowergithub.insites",
                "org.webjars.bowergithub.polymer",
                "org.webjars.bowergithub.polymerelements",
                "org.webjars.bowergithub.vaadin",
                "org.webjars.bowergithub.webcomponents"
        ].each { dep ->
            all*.exclude(group: dep)
        }
    }

}

subprojects {
    apply plugin: 'pmd'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'java-library'
//    apply plugin: 'java-test-fixtures'
    apply plugin: 'io.freefair.lombok'
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.diffplug.spotless"


//    extensions.add('aire', new AireComponent(project))


    spotbugs {
        toolVersion = '4.5.3'
        includeFilter = file("${project.rootDir}/check/spotbugs/exclusions.xml")
    }
    spotbugsMain {
        reports {
            html {
                enabled = true
            }
        }
    }

    java {
        sourceCompatibility = '17'
        modularity.inferModulePath = true
    }

    spotbugsTest {
        enabled = false
    }

    configurations {
        aireComponent {
            transitive false
        }
    }

    pmd {
        toolVersion = '6.34.0'
        sourceSets = [sourceSets.main]
        ruleSets = [
                "${project.rootDir}/check/pmd/rules/errorprone.xml"
        ]
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            googleJavaFormat('1.12.0')
        }
    }


    publishing {

        repositories {
            mavenLocal()

            maven {
                credentials {
                    username "$mavenRepositoryUsername"
                    password "$mavenRepositoryPassword"
                }
                url "$mavenRepositoryUrl"
            }
        }


        task testJar(type: Jar) {
            archiveClassifier = 'tests'
            from sourceSets.test.output
        }

        task javadocJar(type: Jar) {
            from javadoc
            archiveClassifier = 'javadoc'
        }


        publications {
            pluginMaven(MavenPublication) {
                /**
                 * components source
                 */
                from components.java

                /**
                 * maven coordinates
                 */
                groupId = project.group
                artifactId = project.name
                version = project.version

                /**
                 * included artifacts
                 */

                artifact testJar
                artifact javadocJar

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }


    dependencies {


        testImplementation 'org.mockito:mockito-inline'
        testImplementation 'org.junit-pioneer:junit-pioneer'
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        [
                'api',
                'engine'

        ].each { dep ->
            testImplementation "org.junit.jupiter:junit-jupiter-$dep"
            testImplementation "org.junit.platform:junit-platform-suite-$dep"
        }
    }


}
